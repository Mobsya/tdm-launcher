/*
	Aseba - an event-based framework for distributed robot control
	Copyright (C) 2007--2011:
		Stephane Magnenat <stephane at magnenat dot net>
		(http://stephane.magnenat.net)
		and other contributors, see authors.txt for details

	This program is free software: you can redistribute it and/or modify
	it under the terms of the GNU Lesser General Public License as published
	by the Free Software Foundation, version 3 of the License.

	This program is distributed in the hope that it will be useful,
	but WITHOUT ANY WARRANTY; without even the implied warranty of
	MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
	GNU Lesser General Public License for more details.

	You should have received a copy of the GNU Lesser General Public License
	along with this program. If not, see <http://www.gnu.org/licenses/>.
*/


Unicode true


  !define VERSION "2.4.0"


;--------------------------------
;Includes

  !include "MUI2.nsh"		; Modern UI 2
  !include LogicLib.nsh		; ${If} statement
  !include "WinVer.nsh"		; To test the Windows version
  !define REGISTRY_KEY "Software\Mobsya\ThymioDeviceManager"
  !include "x64.nsh"		; x64 support
  !include "NSIS.definitions.nsh.in"
  !include "FileFunc.nsh"
  !AddPluginDir ".\nsis-plugin\x86-unicode"
  ; Compression
  SetCompressor /SOLID lzma

;--------------------------------
;Defines
  !define DRV_SRC ".\thymio-drivers"
  !define DRV_INF_WIN ".\thymio-drivers\drivers\win_xp-7"
  !define DRV_INF_WIN8 ".\thymio-drivers\drivers\win_8"

  ShowInstDetails show
  ShowUninstDetails show

  ;Get installation folder from registry if available
  InstallDirRegKey HKCU ${REGISTRY_KEY} ""

  ;Remember the language selection
  !define MUI_LANGDLL_REGISTRY_ROOT "HKCU"
  !define MUI_LANGDLL_REGISTRY_KEY ${REGISTRY_KEY}
  !define MUI_LANGDLL_REGISTRY_VALUENAME "Language"

  RequestExecutionLevel admin

;--------------------------------
;Variables

Var AlreadyInstalled

;--------------------------------
;Interface Settings

  !define MUI_ABORTWARNING

;--------------------------------
;Language macros (from http://nsis.sourceforge.net/Creating_language_files_and_integrating_with_MUI)

!macro LANG_LOAD LANGLOAD
  !insertmacro MUI_LANGUAGE "${LANGLOAD}"
  !include "translations\${LANGLOAD}.nsh"
  !undef LANG
!macroend

!macro LANG_STRING NAME VALUE
  LangString "${NAME}" "${LANG_${LANG}}" "${VALUE}"
!macroend

!macro LANG_UNSTRING NAME VALUE
  !insertmacro LANG_STRING "un.${NAME}" "${VALUE}"
!macroend

; From: http://nsis.sourceforge.net/CreateInternetShortcut_macro_%26_function
!macro CreateInternetShortcut FILENAME URL ICONFILE ICONINDEX
  WriteINIStr "${FILENAME}.url" "InternetShortcut" "URL" "${URL}"
  WriteINIStr "${FILENAME}.url" "InternetShortcut" "IconFile" "${ICONFILE}"
  WriteINIStr "${FILENAME}.url" "InternetShortcut" "IconIndex" "${ICONINDEX}"
!macroend

!macro CleanShortcuts un
Function ${un}CleanShortcuts
	DetailPrint $(STR_Clean)

	DetailPrint $(STR_Uninst_Menu)
	; Try both current user / all users
	SetShellVarContext all		; current / all. Install program for all users
	Delete "$SMPROGRAMS\Thymio Device Manager.lnk"
	SetShellVarContext current		; current / all. Install program for all users
	Delete "$SMPROGRAMS\Thymio Device Manager.lnk"
	DetailPrint $(STR_Done)
FunctionEnd
!macroend

!insertmacro CleanShortcuts ""
!insertmacro CleanShortcuts "un."

  ;Default installation folder
  InstallDir "${DEFAULT_INSTALL_DIR}"

;--------------------------------
;Pages

  !define MUI_WELCOMEPAGE_TITLE_3LINES		; Add extra space for the title on the welcome page
  !insertmacro MUI_PAGE_WELCOME

  ; Licence
  !define MUI_LICENSEPAGE_TEXT_TOP $(STR_License_Top)
  !insertmacro MUI_PAGE_LICENSE $(license)

  ; Choose directory
  !insertmacro MUI_PAGE_DIRECTORY

  !insertmacro MUI_PAGE_INSTFILES

  !insertmacro MUI_UNPAGE_CONFIRM
  !insertmacro MUI_UNPAGE_INSTFILES

;Translations
  !insertmacro LANG_LOAD "English"			; translations/English.nsh
  !insertmacro LANG_LOAD "French"			; translations/French.nsh
  !insertmacro LANG_LOAD "German"			; translations/German.nsh
  !insertmacro LANG_LOAD "Italian"			; translations/Italian.nsh

; License: only the english license is used, because FSF strongly discourage using an unofficial translation
LicenseLangString license ${LANG_ENGLISH} "license.txt"
LicenseLangString license ${LANG_FRENCH}  "license.txt"
LicenseLangString license ${LANG_GERMAN}  "license.txt"
LicenseLangString license ${LANG_ITALIAN} "license.txt"

;--------------------------------
;Name and file (Defined here to be translation-enabled)

;add exe



SectionGroup /e "-Thymio" GroupThymio
	Section "-TDMLauncher" TDMLauncher
	SetOutPath "$INSTDIR"

    File "TDMLauncher.exe"
	File "thymio-device-manager.exe"

	SectionEnd
	
	Section "-Menus" Menu
		File "org.mobsya.ThymioDeviceManager.ico"

		;Store installation folder
		WriteRegStr HKCU ${REGISTRY_KEY} "" $INSTDIR

		;Create uninstaller
		WriteUninstaller "$INSTDIR\Uninstall.exe"
		WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\ThymioDeviceManager" \
			"DisplayName" "Thymio Device Manager"
		WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\ThymioDeviceManager" \
			"Publisher" "Mobsya Association"
		WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\ThymioDeviceManager" \
			"DisplayVersion" "${VERSION}"
		${GetSize} "$INSTDIR" "/S=0K" $0 $1 $2
			IntFmt $0 "0x%08X" $0
		WriteRegDWORD HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\ThymioDeviceManager" \
			"EstimatedSize" "$0"
		WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\ThymioDeviceManager" \
			"DisplayIcon" "$\"$INSTDIR\org.mobsya.ThymioDeviceManager.ico$\""
		WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\ThymioDeviceManager" \
			"UninstallString" "$\"$INSTDIR\Uninstall.exe$\""

		; Populate the start menu
		${If} $AlreadyInstalled == "true"
			; Clean old shortcuts
			Call CleanShortcuts
		${EndIf}
		SetShellVarContext current
		SetOutPath "$INSTDIR"		; Working directory for the shortcut
		SetShellVarContext all		; current / all. Install program for all users
		; Launcher
			CreateShortCut "$SMPROGRAMS\Thymio Device Manager.lnk" "$INSTDIR\TDMLauncher.exe" "" "$INSTDIR\org.mobsya.ThymioDeviceManager.ico"
			ApplicationID::Set "$SMPROGRAMS\Thymio Device Manager.lnk" "org.mobsya.ThymioDeviceManager"
	SectionEnd

SectionGroupEnd
Section "-Install the driver" InstDriver	; Hidden section -> always executed
	CreateDirectory "$INSTDIR\drivers"
	SetOutPath "$INSTDIR\drivers"

	DetailPrint $(STR_Drv_Install)

	${If} ${AtLeastWin8}
		; Windows 8 or above
		DetailPrint "We are on Windows 8 (or above)"
		File "${DRV_INF_WIN8}\mchpcdc.inf"
		File "${DRV_INF_WIN8}\mchpcdcw.inf"
		File "${DRV_INF_WIN8}\mchpcdc.cat"
		File "${DRV_INF_WIN8}\mchpcdcw.cat"
	${Else}
		; Other windows (XP)
		DetailPrint "We are on Windows XP or above"
		File "${DRV_INF_WIN}\mchpcdc.inf"
		File "${DRV_INF_WIN}\mchpcdcw.inf"
		File "${DRV_INF_WIN}\mchpcdc.cat"
		File "${DRV_INF_WIN}\mchpcdcw.cat"
	${EndIf}

	File "${DRV_SRC}\dpinst32.exe"
	File "${DRV_SRC}\dpinst64.exe"

	${If} ${RunningX64}
		DetailPrint $(STR_Drv_64bits)
		ExecWait '"$INSTDIR\drivers\dpinst64.exe" /c /sa /lm /s /PATH "$INSTDIR\drivers"' $0
	${Else}
		DetailPrint $(STR_Drv_32bits)
		ExecWait '"$INSTDIR\drivers\dpinst32.exe" /c /sa /lm /s /PATH "$INSTDIR\drivers"' $0
	${EndIf}
	DetailPrint "$(STR_Drv_Return_Code) $0"
	IntCmpU $0 0x80000000 drv_error no_error drv_error
	drv_error:
		DetailPrint $(STR_Drv_Problem)
		Goto done
	no_error:
		DetailPrint $(STR_Done)
	done:
SectionEnd


; First function to be executed by the installer
Function .onInit
	; Display the "Language Select" menu
	!insertmacro MUI_LANGDLL_DISPLAY
	InitPluginsDir

	; Check for a previous installation
	ReadRegStr $0 HKCU ${REGISTRY_KEY} ""	; Read installation folder
	${If} $0 != ""
		DetailPrint "Previous installation detected at $0"
		StrCpy $AlreadyInstalled "true"
		MessageBox MB_YESNO $(STR_Previous_Install) /SD IDYES IDNO cancel IDYES ok
		cancel:
			Abort
		ok:
	${Else}
		StrCpy $AlreadyInstalled "false"
	${EndIf}

FunctionEnd

; First function to be executed by the uninstaller
Function un.onInit
  ; Get the language preference from the registry
  !insertmacro MUI_UNGETLANGUAGE
FunctionEnd
 ; Name "@CPACK_NSIS_PACKAGE_NAME@"
 ; OutFile "@CPACK_TOPLEVEL_DIRECTORY@/@CPACK_OUTPUT_FILE_NAME@"

;--------------------------------
;Uninstaller Section

Section "Uninstall"
	; Remove the driver
	${If} ${FileExists} "$INSTDIR\drivers\mchpcdc.inf"
		; Driver was installed
		DetailPrint $(STR_Drv_Uninstall)
		${If} ${RunningX64}
			DetailPrint $(STR_Drv_64bits)
			ExecWait '"$INSTDIR\drivers\dpinst64.exe" /c /u "$INSTDIR\drivers\mchpcdc.inf" /d /q'
			${If} ${FileExists} "$INSTDIR\drivers\mchpcdcw.inf"
				; Driver was installed
				ExecWait '"$INSTDIR\drivers\dpinst64.exe" /c /u "$INSTDIR\drivers\mchpcdcw.inf" /d /q'
			${EndIf}
		${Else}
			DetailPrint $(STR_Drv_32bits)cer
			ExecWait '"$INSTDIR\drivers\dpinst32.exe" /c /u "$INSTDIR\drivers\mchpcdc.inf" /d /q'
			${If} ${FileExists} "$INSTDIR\drivers\mchpcdcw.inf"
				; Driver was installed
				ExecWait '"$INSTDIR\drivers\dpinst32.exe" /c /u "$INSTDIR\drivers\mchpcdcw.inf" /d /q'
			${EndIf}
		${EndIf}

		${If} ${AtLeastWin7}
			; Windows 7 or above
			; We must uninstall our certificate
			DetailPrint "Uninstalling the security certificate..."
			${If} ${RunningX64}
				ExecWait '"$INSTDIR\drivers\certmgr_x64.exe" -del -c -n "Mobsya" -s -r LocalMachine root' $0
			${Else}
				ExecWait '"$INSTDIR\drivers\certmgr_x86.exe" -del -c -n "Mobsya" -s -r LocalMachine root' $0
			${EndIf}
			DetailPrint "Done."
			IntCmp $0 0x0 cert_un_ok cert_un_error cert_un_error
			cert_un_ok:
				DetailPrint "Certificate uninstalled"
				Goto cert_un_done
			cert_un_error:
				DetailPrint "A problem occured"
			cert_un_done:
			; Done
		${EndIf}

		RMDir /r "$INSTDIR\drivers"
		DetailPrint $(STR_Done)
	${EndIf}

	
	;@CPACK_NSIS_EXTRA_UNINSTALL_COMMANDS@
	;Remove files we installed.
	;Keep the list of directories here in sync with the File commands above.
	;@CPACK_NSIS_DELETE_FILES@
	;@CPACK_NSIS_DELETE_DIRECTORIES@

	; Remove the application
	DetailPrint $(STR_Uninst_Folder)
	RMDir /r "$INSTDIR"
	DetailPrint $(STR_Done)

	Call un.CleanShortcuts
	DeleteRegKey HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\ThymioDeviceManager"
	DeleteRegKey HKCU ${REGISTRY_KEY}
	DetailPrint $(STR_Done)
SectionEnd

!verbose 4
;!finalize '"${PYTHON}" "${ASEBA_SOURCE_DIRECTORY}/CMakeModules/sign.py" "%1"'
